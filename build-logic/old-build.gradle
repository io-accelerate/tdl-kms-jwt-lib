//~~~~ Settings for Gradle
buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.1"
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
        classpath 'ch.netzwerg:gradle-release-plugin:1.2.4'
    }

}

apply plugin: 'java'
apply plugin: 'application'

group 'ro.ghionoiu'

//noinspection GroovyUnusedAssignment
targetCompatibility = JavaVersion.VERSION_1_8
//noinspection GroovyUnusedAssignment
sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

// ~~~~~ Dependencies

dependencies {
    compile 'io.jsonwebtoken:jjwt:0.7.0'
    compile 'com.amazonaws:aws-java-sdk-kms:1.11.138'

    compile 'com.beust:jcommander:1.64'
    
    compile 'commons-logging:commons-logging:1.1.1'
    compile 'log4j:log4j:1.2.17'
    compile 'org.slf4j:slf4j-api:1.7.25'
    compile 'ch.qos.logback:logback-classic:1.1.3'
    compile 'org.slf4j:log4j-over-slf4j:1.7.25'
    compile 'org.slf4j:jcl-over-slf4j:1.7.25'
    
    
    compileOnly 'org.projectlombok:lombok:1.16.16'

    testCompile('junit:junit:4.11'){
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
    }
    testCompile 'org.hamcrest:hamcrest-core:1.3'
    testCompile 'org.mockito:mockito-all:1.10.19'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'commons-io:commons-io:2.5'
    testCompile 'net.trajano.commons:commons-testing:1.0.1'
    testCompile 'com.github.stefanbirkner:system-rules:1.16.1'
    testCompileOnly 'org.projectlombok:lombok:1.16.16'
}

// ~~~~~ Test


test {
    // pass the proxy configuration to the gradle test executor
    systemProperty "http.proxyHost", System.getProperty('http.proxyHost')
    systemProperty "http.proxyPort", System.getProperty('http.proxyPort')
    systemProperty "https.proxyHost", System.getProperty('https.proxyHost')
    systemProperty "https.proxyPort", System.getProperty('https.proxyPort')
}

// ~~~~~ Packaging

apply plugin: 'com.github.johnrengelman.shadow'

mainClassName = "ro.ghionoiu.kmsjwt.GenerateTokenApp"
jar {
    manifest {
        attributes "Main-Class": "$mainClassName"
    }
}


// ~~~~~ Gather coverage

apply plugin: "jacoco"
apply plugin: "com.github.kt3k.coveralls"

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

//~~~~~ Release management

/**
 * Prerequisites
 *    - Create package in Bintray
 *
 * Now you can use regular maven upload to push files to you package.
 * The version will be created automatically
 *
 * To push to Maven Central use Bintray sync features:
 *    - Created request for jcenter ( if missing )
 *    - Manually sync the two repos
 *
 * To publish new version use the Bintray UI:
 *    - publish current artifacts
 *    - sync with Maven Central
 */
apply plugin: "ch.netzwerg.release"
apply plugin: 'maven'

release {
    dependsOn build // 'distZip' could e.g be used in combination with the 'application' plugin
    push = false // 'true' would e.g. be useful when triggering the release task on a CI server
    versionSuffix = '-SNAPSHOT' // '.DEV' or '' (empty) could be useful alternatives
    tagPrefix = 'v' // 'r' or '' (empty) could be useful alternatives
}

private static String readSystem(String property) {
    System.getenv(property) != null  ? System.getenv(property) : "unset"
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// Add the sourceJars to non-extractor modules
artifacts {
    archives sourceJar
    archives javadocJar
}

uploadArchives {
    repositories.mavenDeployer {
        name = 'bintray-julianghionoiu-kms-jwt'
        repository(id: 'bintray-julianghionoiu-kms-jwt',
                url: 'https://api.bintray.com/maven/julianghionoiu/maven/kms-jwt') {
            authentication(userName: readSystem("BINTRAY_USER"), password: readSystem("BINTRAY_KEY"))
        }

        pom.project {
            name 'KMS JWT'
            description 'A Java library to sign and verify JSON Web Tokens (JWT) using Amazon Key Management Service (KMS)'
            url 'https://github.com/julianghionoiu/kms-jwt'

            developers {
                developer {
                    name 'Iulian (Julian) Ghionoiu'
                    email 'iulian.ghionoiu@gmail.com'
                    organization 'Iulian Ghionoiu'
                    organizationUrl 'https://github.com/julianghionoiu'
                }
            }

            scm {
                connection 'scm:git:git@github.com:julianghionoiu/kms-jwt.git'
                developerConnection 'scm:git:git@github.com:julianghionoiu/kms-jwt.git'
                url 'git@github.com:julianghionoiu/kms-jwt.git'
            }

            licenses {
                license {
                    name 'Apache License, Version 2.0'
                    url 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                }
            }
        }
    }
}